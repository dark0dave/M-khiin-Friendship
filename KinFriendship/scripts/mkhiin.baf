// Timer-Based Talks
IF
    InParty(Myself)
    Global("BFSKinFriendshipActive","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("BFSKinFriendshipTalksTimer","GLOBAL",1200)
    SetGlobal("BFSKinFriendshipTalks","GLOBAL",1)
    SetGlobal("BFSKinFriendshipActive","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    RealGlobalTimerExpired("BFSKinFriendshipTalksTimer","GLOBAL")
	!StateCheck(Player1,CD_STATE_NOTVALID)    // CamDawg's conditions for no charm etc
    !StateCheck(Myself,CD_STATE_NOTVALID)
    CombatCounter(0)                          // Not in battle please
    !See([ENEMY])
    Global("BFSKinFriendshipActive","GLOBAL",1)
    OR(4)
      Global("BFSKinFriendshipTalks","GLOBAL",1)
      Global("BFSKinFriendshipTalks","GLOBAL",3)
      Global("BFSKinFriendshipTalks","GLOBAL",5)
      Global("BFSKinFriendshipTalks","GLOBAL",7)
THEN
  RESPONSE #100
    IncrementGlobal("BFSKinFriendshipTalks","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    Global("BFSKinFriendshipActive","GLOBAL",1)
    CombatCounter(0)
    !See([ENEMY])
    OR(4)
      Global("BFSKinFriendshipTalks","GLOBAL",2)
      Global("BFSKinFriendshipTalks","GLOBAL",4)
      Global("BFSKinFriendshipTalks","GLOBAL",6)
      Global("BFSKinFriendshipTalks","GLOBAL",8)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END

// After meeting Caelar at the bridge 
// THANK YOU JASTEY! <3 

IF
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck("MKHIIN",CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	GlobalGT("bd_plot","global",169)
	!See("bdcaelar")
	AreaCheck("bd1000")
	Global("BFSMkBridge","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("BFSMkBridge","GLOBAL",1)
END

IF
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("BFSMkBridge","GLOBAL",1)
THEN
	RESPONSE #100
	MoveToObject(Player1)
	StartDialogNoSet(Player1)
END

// Dragon Cave Warning

IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
AreaCheck("BD7210")           
Global("BFSMkDragon","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BFSMkDragon","GLOBAL",1)
END

IF
  Global("BFSMkDragon","GLOBAL",1)
 THEN
 RESPONSE #100
  MoveToObject(Player1)
  Dialogue(Player1)
END 

// Dragon Dead
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
Dead("BDMORENT")          
Global("BFSMkDragon","GLOBAL",2)
THEN
RESPONSE #100
SetGlobal("BFSMkDragon","GLOBAL",3)
END

IF
  Global("BFSMkDragon","GLOBAL",3)
 THEN
 RESPONSE #100
  MoveToObject(Player1)
  Dialogue(Player1)
END 


// Bhaal Temple - Mindflayer Dead
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
Dead("BDDARSKH")          
Global("BFSMkBhaal","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BFSMkBhaal","GLOBAL",1)
END

IF
  Global("BFSMkBhaal","GLOBAL",1)
 THEN
 RESPONSE #100
  MoveToObject(Player1)
  Dialogue(Player1)
END 

// Boareskyr Bridge Fallout
// THANK YOU JASTEY! <3 
/* activate - no The Boareskyr Bridge Scene mod installed */
IF
	InParty("MKHIIN")
	// !RealGlobalTimerNotExpired("BFSMkDialogueTimer","GLOBAL")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	GlobalGT("bd_plot","global",294) //after it happened
	GlobalLT("BFSMkBoareskyr","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("BFSMkBoareskyr","GLOBAL",3)
END
/* initiate */
IF
	InParty("MKHIIN")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	Global("BFSMkBoareskyr","GLOBAL",3)
THEN
	RESPONSE #100
		// RealSetGlobalTimer("BFSMkDialogTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END

// Jegg Made M'Khiin Armor 
IF
  HasItem("bdchan01",Myself)
  Global("BFSMkArmor","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("BFSMkArmor","GLOBAL",1)
    StartDialogNoSet([PC])
END

// Post meeting with other goblins
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
OR(2) Global("BDSDD251_TALK","GLOBAL",2) Global("BD_SDD251","GLOBAL",4)
Global("BFSMkGobbos","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BFSMkGobbos","GLOBAL",1)
END

IF
  Global("BFSMkGobbos","GLOBAL",1)
 THEN
 RESPONSE #100
  MoveToObject(Player1)
  Dialogue(Player1)
END 

// Post Diplomacy Meeting with Caelar
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
AreaCheck("bd7300")
Global("bd_plot","global",393)
Global("BFSMkAggressiveNegotations","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BFSMkAggressiveNegotations","GLOBAL",1)
END

IF
  Global("BFSMkAggressiveNegotations","GLOBAL",1)
 THEN
 RESPONSE #100
  MoveToObject(Player1)
  Dialogue(Player1)
END 

// Post Avernus

IF
	InParty(Myself)
	AreaCheck("BD4300")
	GlobalGT("bd_plot","global",589)
	Global("BFSMkAvernus","GLOBAL",0)
	CombatCounter(0)
THEN
RESPONSE #100
	SetInterrupt(FALSE)
	Wait(1)
	SetGlobal("BFSMkAvernus","GLOBAL",1)
	StartDialogueNoSet(Player1)
	SetInterrupt(TRUE)
END
